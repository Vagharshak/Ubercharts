
apply plugin: 'java'    // adds Java plugin for common Java tasks like 'compile'
apply plugin: 'jetty'   // adds Jetty plugin for testing without needing a special web server
apply plugin: 'idea'    // adds IDEA plugin to enable the creation of an IntellJ project
apply plugin: 'eclipse' // adds IDEA plugin to enable the creation of an Eclipse project

/* This sets the needed maven repositories */
repositories {
  mavenCentral() // The default maven repo
  mavenRepo urls: "http://google-gson.googlecode.com/svn/mavenrepo" // The maven repo for gson
}

/*
  It's a basic concept of Wicket that html files are located alongside
  Java files within the src folder. The Gradle Java plugin, however, does not
  copy those html files to the compile target location by default.

  The following lines are therefore needed to ensure, that the html files,
  which are located alongside Java files, are copied to the target folder
  alongside the appropriate class files.
*/
sourceSets {
  main {
    java {
      srcDir 'src/main/java'
    }
    resources {
      srcDir 'src/main/resources'
      srcDir 'src/main/java'
    }
  }
}



dependencies {

  /*
   These are the dependencies, which are needed to compile and run the project.
  */
  compile(
          [group: 'org.apache.wicket', name: 'wicket', version: '1.4.15'],
          [group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.5.8'],          

          /*
          * There is a problem with the public gson maven repository.
          * The following line will be disabled until the repo gets fixed.
          *
          * [group: 'com.google.code', name: 'gson', version: '1.6'],
          *
          *  Instead, the gson jar will be loaded from the local libs folder.
          */
          [fileTree(dir: 'libs', include: '*.jar')]
  )

  /*
   These are the dependencies, which are needed to test the project.
  */
  testCompile(
          [group: 'org.mortbay.jetty', name: 'jetty-util', version: '6.1.25'],
          [group: 'org.mortbay.jetty', name: 'jetty-management', version: '6.1.25'],
          [group: 'junit', name: 'junit', version: '4.8.1'],
  )
}


